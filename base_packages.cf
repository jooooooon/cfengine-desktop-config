bundle agent base_packages {
 vars:
	ubuntu::
		"thunderbird"       string => "thunderbird";
		"thunderbird_l10n"  string => "locale";

	debian.!ubuntu::
		"thunderbird"       string => "icedove";
		"thunderbird_l10n"  string => "l10n";

		"distro_specific_packages" slist => { "$(thunderbird)-nostalgy" };

	any::

	"package_list_shell" slist => {
			"vim",
			"tree",
			"htop", "iotop",
			"rsync",
			"git-core", "gitk",
			"bsdgames",
			"apt-file", "python-software-properties",
			"ldap-utils",
		};

	"package_list_apps" slist => {
			"terminator",
			"$(thunderbird)",
			"$(thunderbird)-$(thunderbird_l10n)-en-gb",
			"$(thunderbird)-$(thunderbird_l10n)-fr",
			"enigmail",
			"network-manager-openvpn-gnome",
			"virt-manager",
			"gimp",
			"wireshark",
			"vlc",
		};

	"install_packages"
		slist  => { @(package_list_shell), @(package_list_apps), @(distro_specific_packages) },
		policy => "ifdefined";

	"remove_packages"
		slist => { "gnumeric", "abiword" };

 classes:
	"install_wireless_firmware" expression => returnszero("/usr/bin/lspci | grep 'Network controller: Intel Corporation Centrino Wireless-N 1000'", "useshell");
	"need_debian_non_free_repo"         or => { "install_wireless_firmware" };

 packages:
	"$(install_packages)"
		package_method  => generic,
		package_policy  => "addupdate",
		action          => if_elapsed(120); # only try updating every few hours, that's already plenty!

	"$(remove_packages)"
		package_method  => generic,
		package_policy  => "delete",
		action          => if_elapsed(120); # only try updating every few hours, that's already plenty!

	"firmware-iwlwifi"
		package_method  => generic,
		package_policy  => "addupdate",
		ifvarclass      => "debian_non_free_repo_added",
		action          => if_elapsed(120); # only try updating every few hours, that's already plenty!
		

commands:
	"/usr/bin/apt-add-repository 'http://ftp.fr.debian.org/debian/ non-free'"
		ifvarclass      => "need_debian_non_free_repo",
		classes         => if_repaired("debian_non_free_repo_added"),
		action          => if_elapsed(120); # only try updating every few hours, that's already plenty!
	
	"/usr/bin/aptitude update"
		ifvarclass      => "debian_non_free_repo_added";
}
